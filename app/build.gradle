plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
    alias libs.plugins.dagger.hilt.android
    alias libs.plugins.kotlin.kapt
}
android {
    compileSdk 33

    defaultConfig {
        applicationId "com.kylobytes.monetrail"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            versionNameSuffix "-dev01"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.get()
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'com.kylobytes.monetrail'
}

dependencies {
    implementation libs.android.ktx
    implementation libs.collection.ktx

    def composeBom = platform(libs.compose.bom)
    implementation composeBom
    implementation libs.activity.compose

    implementation libs.navigation.compose
    implementation libs.navigation.ui.ktx

    implementation libs.lifecycle.runtime.ktx
    implementation libs.lifecycle.viewmodel.ktx
    implementation libs.lifecycle.viewmodel.compose
    implementation libs.lifecycle.viewmodel.savedstate
    kapt libs.lifecycle.compiler

    implementation libs.room.runtime
    implementation libs.room.ktx
    kapt libs.room.runtime

    implementation libs.hilt.android
    implementation libs.hilt.navigation.compose
    kapt libs.hilt.compiler

    testImplementation libs.junit

    androidTestImplementation libs.hilt.android.testing
    kaptAndroidTest libs.hilt.compiler
    testImplementation libs.hilt.android.testing
    kaptTest libs.hilt.compiler

    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

    androidTestImplementation composeBom
    androidTestImplementation libs.navigation.testing
}

kapt {
    correctErrorTypes true
}